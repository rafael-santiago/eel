#
#                        Copyright (C) 2006, 2016 by Rafael Santiago
#
# This is free software. You can redistribute it and/or modify under
# the terms of the GNU General Public License version 2.
#
#

include ~/toolsets/gcc/gcc-app.hsl

var sources type list;
var includes type list;
var cflags type list;
var libraries type list;
var ldflags type list;

project eel-unit : toolset "gcc-c-app" : $sources, $includes, $cflags, $libraries, $ldflags, "eel-tests";

eel-unit.prologue() {
    $sources.ls(".*\\.c$");
    $includes.add_item("cutest/src");
    $libraries.add_item("../lib");
    $ldflags.add_item("-leel");
    $libraries.add_item("cutest/src/lib");
    $ldflags.add_item("-lcutest");
    if (hefesto.sys.cd("cutest/src")) {
        hefesto.sys.forge("cutest", "Forgefile.hsl", "--Forgefile-projects=cutest --obj-output-dir=obj --bin-output-dir=lib");
        hefesto.sys.cd("../..");
        if (hefesto.sys.last_forge_result() != 0) {
            hefesto.project.abort(1);
        }
    }

    if (isdir("/usr/local/include")) {
        $includes.add_item("/usr/local/include");
    }

    if (isdir("/usr/local/lib")) {
        $libraries.add_item("/usr/local/lib");
    }

    if (hefesto.sys.os_name() == "linux") {
        $ldflags.add_item("-ldl");
        $ldflags.add_item("-lpthread");
    } else if (hefesto.sys.os_name() == "freebsd") {
        $ldflags.add_item("-lexecinfo");
        $ldflags.add_item("-lpthread");
    } else if (hefesto.sys.os_name() == "openbsd") {
        $ldflags.add_item("-lexecinfo");
        $ldflags.add_item("-lpthread");
    }
}

eel-unit.epilogue() {
    if (hefesto.sys.last_forge_result() == 0) {
        var exit_code type int;
        var eel_tests_path type string;

        if (hefesto.sys.os_name() != "windows") {
            $eel_tests_path = "bin/eel-tests";
        } else {
            $eel_tests_path = "bin\\eel-tests.exe";
        }

        $exit_code = hefesto.sys.run($eel_tests_path);

        if ($exit_code != 0) {
            hefesto.project.abort($exit_code);
        }
    }
}
